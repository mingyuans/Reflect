group 'com.mingyuans.reflect'
version '1.0.0'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: "com.jfrog.artifactory"

sourceCompatibility = 1.6
targetCompatibility = 1.6

ext {
    siteUrl = "http://www.mingyuans.me"
    gitUrl = "https://github.com/mingyuans/Reflect.git"
}


repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

install {
    repositories.mavenInstaller {
        pom {
            artifactId = project.name
            project {
                packaging 'jar'
                url project.ext.siteUrl
                developers {
                    developer {
                        id 'mingyuans'
                        name 'mingyuans'
                        email 'yanxqwork@gmail.com'
                    }
                }
                scm {
                    connection project.ext.gitUrl
                    developerConnection project.ext.gitUrl
                    url project.ext.siteUrl
                }
            }
        }
    }
}


Properties properties = new Properties()
if (project.rootProject.file("local.properties").exists()) {
    properties.load(project.rootProject.file("local.properties").newInputStream())
}
bintray {
    user = properties.getProperty("bintray.user","")
    key = properties.getProperty("bintray.apikey","")
    configurations = ['archives']
    override = true
    pkg {
        repo = "maven"
//        name = isSnapshot()? project.ext.artifactId + "-snapshot" : project.ext.artifactId;
        name = "reflect"
        licenses = ["MIT"]
        websiteUrl = project.siteUrl
        vcsUrl = project.gitUrl
        publish = true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

artifactory {
    contextUrl = 'http://oss.jfrog.org/artifactory'
    publish {
        repository {
            repoKey = 'oss-snapshot-local';
            username = properties.getProperty("bintray.user","")
            password = properties.getProperty("bintray.apikey","")
            maven = true
        }

        Defaults {
            publishConfigs('archives')
            publishIvy = false
        }
    }
}